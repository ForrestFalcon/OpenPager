apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply plugin: "com.jaredsburrows.license"
apply plugin: 'idea'

licenseReport {
    generateHtmlReport = true
    generateJsonReport = true

    copyHtmlReportToAssets = true
    copyJsonReportToAssets = false
}

// CI
def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

idea {
    module {
        // if you hate browsing Javadoc
        downloadJavadoc = false

        // and love reading sources :)
        downloadSources = true
    }
}

android {
    compileSdkVersion extCompileSdkVersion
    buildToolsVersion extBuildToolsVersion
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        javaMaxHeapSize "4g"

        // Skip pre-dexing when running on CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isCi
    }
    defaultConfig {
        applicationId "de.openfiresource.openpager"
        minSdkVersion 21
        targetSdkVersion extCompileSdkVersion
        versionCode 7
        versionName '1.0.0'
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    lintOptions {
        htmlReport true
        htmlOutput file("lint-report.html")
        textOutput 'stdout'
        textReport true
        abortOnError true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'org.osmdroid:osmdroid-android:6.0.1'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.chauthai.swipereveallayout:swipe-reveal-layout:1.4.1'
    implementation 'com.github.medyo:android-about-page:1.2.4'

    implementation 'gun0912.ted:tedpermission-rx2:2.2.0'

    implementation 'br.com.simplepass:loading-button-android:1.14.0'

    /**
     * Retrofit
     */
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'

    /**
     * Timber logging
     */
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.orhanobut:logger:1.15'

    /**
     * Icons
     */
    implementation 'com.mikepenz:iconics-core:3.0.2@aar'
    implementation 'com.mikepenz:google-material-typeface:2.2.0.3.original@aar'

    /**
     * Google
     */
    implementation "com.android.support:appcompat-v7:$extSupportLibraryVersion"
    implementation "com.android.support:design:$extSupportLibraryVersion"
    implementation "com.android.support:support-v4:$extSupportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$extSupportLibraryVersion"
    implementation "com.android.support:support-annotations:$extSupportLibraryVersion"
    implementation "com.android.support:customtabs:$extSupportLibraryVersion"

    /**
     * Play services
     */
    implementation 'com.google.firebase:firebase-core:16.0.4'
    implementation 'com.google.firebase:firebase-messaging:17.3.3'
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'com.google.android.gms:play-services-analytics:16.0.4'
    implementation 'com.google.android.gms:play-services-auth:16.0.1'

    /**
     * RxJava
     */
    implementation 'io.reactivex.rxjava2:rxjava:2.1.11'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
    implementation 'nl.littlerobots.rxlint:rxlint:1.6.1'
    implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0'

    /**
     * Dagger
     */
    implementation 'com.google.dagger:dagger:2.15'
    implementation('com.google.dagger:dagger-android:2.15', {
        exclude group: 'com.google.code.findbugs'
    })
    implementation('com.google.dagger:dagger-android-support:2.15', {
        exclude group: 'com.google.code.findbugs'
    })
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.15'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.15'

    /**
     * Architecture Components
     */

    // Lifecycle
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    annotationProcessor "android.arch.lifecycle:common-java8:1.1.1"

    // Room
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"

    // Java8 support for Lifecycles
    implementation 'android.arch.lifecycle:common-java8:1.1.1'

    // RxJava support for Room (use 1.1.0-beta1 for latest alpha)
    implementation 'android.arch.persistence.room:rxjava2:1.1.1'

    // ReactiveStreams support for LiveData
    implementation 'android.arch.lifecycle:reactivestreams:1.1.1'

    /**
     * Tests
     */
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.22.0'
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'

    // Test helpers for LiveData
    testImplementation 'android.arch.core:core-testing:1.1.1'

    // Test helpers for Room
    testImplementation 'android.arch.persistence.room:testing:1.1.1'

    /**
     * LeakCanary
     */
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}

apply plugin: 'com.google.gms.google-services'
